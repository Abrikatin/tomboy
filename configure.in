AC_INIT(Tomboy/Tomboy.cs)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(tomboy, 0.2.2)
AM_MAINTAINER_MODE
AC_PROG_INTLTOOL([0.25])

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
AM_PROG_LIBTOOL

#
# Setup GETTEXT
#
ALL_LINGUAS="ca de en_CA en_GB es fr hu is ja nl pl pt_BR sq sv"
GETTEXT_PACKAGE=tomboy
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Gettext package])
AM_GLIB_GNU_GETTEXT

#
# Find gconftool-2
#
AC_PATH_PROG(GCONFTOOL, gconftool-2)
AM_GCONF_SOURCE_2

#
# Find pkg-config
#
AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test "x$PKG_CONFIG" = "xno"; then
	AC_MSG_ERROR([You need to install pkg-config])
fi

#
# Find mcs
#
AC_PATH_PROG(MCS, mcs)
if test "x$MCS" = "x" ; then
	AC_MSG_ERROR([Can't find "mcs" in your PATH])
fi

#
# Gdk and Atk are needed for libtrayicon
#
GDK20_MINIMUM_VERSION=2.2.3
ATK_MINIMUM_VERSION=1.2.4

PKG_CHECK_MODULES(LIBTOMBOY, 
		  gdk-2.0 >= $GDK20_MINIMUM_VERSION
		  atk >= $ATK_MINIMUM_VERSION)
AC_SUBST(LIBTOMBOY_CFLAGS)
AC_SUBST(LIBTOMBOY_LIBS)

#
# Need libpanelapplet to create the applet
#
PKG_CHECK_MODULES(LIBPANELAPPLET, libpanelapplet-2.0)

#
# Check for GtkSpell here, as we call into it from C#.
# http://gtkspell.sourceforge.net/
#
PKG_CHECK_MODULES(GTKSPELL, gtkspell-2.0)
PKG_CHECK_MODULES(GTKSPELL, gtkspell-2.0 > 2.0.5, OLD_GTKSPELL="no", OLD_GTKSPELL="yes")
PKG_CHECK_MODULES(GTKSPELL, gtkspell-2.0 >= 2.0.9, FIXED_GTKSPELL="yes", FIXED_GTKSPELL="no")
AM_CONDITIONAL(OLD_GTKSPELL, test "$OLD_GTKSPELL" = "yes")
AM_CONDITIONAL(FIXED_GTKSPELL, test "$FIXED_GTKSPELL" = "yes")

#
# Use DBus# to export our remote control if available.
#
PKG_CHECK_MODULES(DBUS, dbus-sharp, ENABLE_DBUS="yes", ENABLE_DBUS="no")
AM_CONDITIONAL(ENABLE_DBUS, test "$ENABLE_DBUS" = "yes")
AC_SUBST(DBUS_LIBS)

#
# Tomboy.exe needs Gtk# Gnome# GConf#
#
PKG_CHECK_MODULES(TOMBOY, gtk-sharp gnome-sharp gconf-sharp)
AC_SUBST(TOMBOY_LIBS)

#
# Properly handle libraries that aren't in our standard GAC path.
#
DBUS_PREFIX=`$PKG_CONFIG --variable=prefix dbus-sharp`
EXTRA_GAC_PREFIX="$DBUS_PREFIX"
AC_SUBST(EXTRA_GAC_PREFIX)


AC_OUTPUT([
Makefile
tomboy.spec
data/Makefile
libtomboy/Makefile
Tomboy/Makefile
po/Makefile.in
])
