AC_INIT(Tomboy/Tomboy.cs)
AM_INIT_AUTOMAKE(tomboy, 0.3.5)
AM_MAINTAINER_MODE

AM_CONFIG_HEADER(config.h)

AC_PROG_INSTALL
AC_PROG_INTLTOOL([0.25])
AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC

AM_DISABLE_STATIC
AM_PROG_LIBTOOL

#
# Setup GETTEXT
#
ALL_LINGUAS="bg ca cs da de en_CA en_GB es eu fi fr hu is it ja ko lt nb ne nl no pa pl pt_BR ro ru rw sq sv tr uk vi zh_CN"
GETTEXT_PACKAGE=tomboy
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Gettext package])
AM_GLIB_GNU_GETTEXT

#
# Find gconftool-2
#
AC_PATH_PROG(GCONFTOOL, gconftool-2)
AM_GCONF_SOURCE_2

#
# Find mcs
#
AC_PATH_PROG(MCS, mcs)
if test "x$MCS" = "x" ; then
	AC_MSG_ERROR([Can't find "mcs" in your PATH])
fi

#
# Gdk and Atk are needed for libtrayicon
#
GDK20_MINIMUM_VERSION=2.2.3
GTK20_MINIMUM_VERSION=2.2.3
ATK_MINIMUM_VERSION=1.2.4

PKG_CHECK_MODULES(LIBTOMBOY, 
		  libgnomeprint-2.2
		  libgnomeprintui-2.2
		  gdk-2.0 >= $GDK20_MINIMUM_VERSION
		  gtk+-2.0 >= $GTK20_MINIMUM_VERSION
		  atk >= $ATK_MINIMUM_VERSION)
AC_SUBST(LIBTOMBOY_CFLAGS)
AC_SUBST(LIBTOMBOY_LIBS)

#
# Need Gtk 2.6+ for gdk_x11_window_set_user_time() to get around 
# anti-focus avoidance for keybindings.
#
PKG_CHECK_MODULES(GTK, gtk+-2.0 >= 2.6.0, GTK_2_6="yes", GTK_2_6="no")
if test "x$GTK_2_6" = "xyes"; then
   AC_DEFINE(HAVE_GDK_X11_WINDOW_SET_USER_TIME, 1, 
             [Whether GTK+ method gdk_x11_window_set_user_time() is present.])
fi

#
# Need libpanelapplet to create the applet
#
PKG_CHECK_MODULES(LIBPANELAPPLET, libpanelapplet-2.0)

PANELAPPLET_SERVER_DIR=`$PKG_CONFIG --variable=libdir libpanelapplet-2.0`
if test "x$PANELAPPLET_SERVER_DIR" = "x"; then
   PANELAPPLET_SERVER_DIR="$libdir/bonobo/servers"
else
   PANELAPPLET_SERVER_DIR="$PANELAPPLET_SERVER_DIR/bonobo/servers"
fi
AC_SUBST(PANELAPPLET_SERVER_DIR)

#
# Check for GtkSpell here, as we call into it from C#.
# http://gtkspell.sourceforge.net/
#
PKG_CHECK_MODULES(GTKSPELL, gtkspell-2.0)
PKG_CHECK_MODULES(GTKSPELL, gtkspell-2.0 > 2.0.5, OLD_GTKSPELL="no", OLD_GTKSPELL="yes")
PKG_CHECK_MODULES(GTKSPELL, gtkspell-2.0 >= 2.0.9, FIXED_GTKSPELL="yes", FIXED_GTKSPELL="no")
AM_CONDITIONAL(OLD_GTKSPELL, test "$OLD_GTKSPELL" = "yes")
AM_CONDITIONAL(FIXED_GTKSPELL, test "$FIXED_GTKSPELL" = "yes")

#
# Use DBus# to export our remote control if available.
#
PKG_CHECK_MODULES(DBUS, dbus-sharp >= 0.23.2, ENABLE_DBUS="yes", ENABLE_DBUS="no")
AC_ARG_ENABLE(dbus, 
	      [  --enable-dbus[[=no/yes]]    compile with dbus support [[default: yes]]],
	      ENABLE_DBUS="$enableval")
AM_CONDITIONAL(ENABLE_DBUS, test "$ENABLE_DBUS" = "yes")
AC_SUBST(DBUS_LIBS)

#
# Tomboy.exe needs Gtk# Gnome# GConf#
#
PKG_CHECK_MODULES(TOMBOY, gtk-sharp gnome-sharp gconf-sharp)
AC_SUBST(TOMBOY_LIBS)

#
# Evolution.dll needs Gmime#
#
PKG_CHECK_MODULES(EVOLUTION, gmime-sharp, ENABLE_EVOLUTION="yes", ENABLE_EVOLUTION="no")
AC_ARG_ENABLE(evolution, 
	      [  --enable-evolution[[=no/yes]]  compile evolution plugin [[default: yes]]],
	      ENABLE_EVOLUTION="$enableval")
AM_CONDITIONAL(ENABLE_EVOLUTION, test "$ENABLE_EVOLUTION" = "yes")
AC_SUBST(EVOLUTION_LIBS)

#
# GalagoPresence.dll needs Galago#
#
PKG_CHECK_MODULES(GALAGO, galago-sharp, ENABLE_GALAGO="yes", ENABLE_GALAGO="no")
AC_ARG_ENABLE(galago, 
	      [  --enable-galago[[=no/yes]]  compile galago plugin [[default: yes]]],
	      ENABLE_GALAGO="$enableval")
AM_CONDITIONAL(ENABLE_GALAGO, test "$ENABLE_GALAGO" = "yes")
AC_SUBST(GALAGO_LIBS)

#
# Properly handle libraries that aren't in our standard GAC path.
#
DBUS_PREFIX=`$PKG_CONFIG --variable=prefix dbus-sharp`
EXTRA_GAC_PREFIX="$DBUS_PREFIX"
AC_SUBST(EXTRA_GAC_PREFIX)


AC_OUTPUT([
Makefile
tomboy.spec
data/Makefile
data/tomboy-plugins.pc
data/images/Makefile
libtomboy/Makefile
Tomboy/Makefile
Tomboy/Plugins/Makefile
po/Makefile.in
])
